plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id "com.matthewprenger.cursegradle" version "1.4.0"
    id "com.modrinth.minotaur" version "2.+"
}

architectury {
    platformSetupLoomIde()
    forge()
}

loom {
    accessWidenerPath = project(":common").loom.accessWidenerPath

    forge {
        convertAccessWideners = true
        extraAccessWideners.add loom.accessWidenerPath.get().asFile.name

        mixinConfig "grieflogger-common.mixins.json"
        mixinConfig "grieflogger.mixins.json"
    }
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin since it *excludes* files.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common
}

dependencies {
    forge "net.minecraftforge:forge:${rootProject.forge_version}"
    // Remove the next line if you don't want to depend on the API
    modApi "dev.architectury:architectury-forge:${rootProject.architectury_version}"

    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionForge")) { transitive = false }

    modImplementation "org.xerial:sqlite-jdbc:3.44.1.0"
    shadowCommon "org.xerial:sqlite-jdbc:3.44.1.0"

    modImplementation "mysql:mysql-connector-java:8.0.33"
    shadowCommon "mysql:mysql-connector-java:8.0.33"

    modImplementation "curse.maven:supermartijn642s-config-lib-438332:${project.config_library_file_forge}"
}

processResources {
    inputs.property "version", project.version
    inputs.property "config_library_version", config_library_version

    filesMatching("META-INF/mods.toml") {
        expand([
                "version": project.version,
                "config_library_version": config_library_version
        ])
    }
}

shadowJar {
    exclude "fabric.mod.json"
    exclude "architectury.common.json"
    exclude "org/slf4j/**"

    configurations = [project.configurations.shadowCommon]
    archiveClassifier = "dev-shadow"
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    archiveClassifier = "forge"
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

remapSourcesJar {
    //noinspection GroovyAccessibility
    archiveClassifier = "forge-sources"
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

publishing {
    publications {
        mavenForge(MavenPublication) {
            artifactId = rootProject.archives_base_name + "-" + project.name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
    }
}

curseforge {
    apiKey = System.getenv("CURSEFORGE_API_KEY")
    project {
        id = project.curse_forge_project_id
        releaseType = project.curse_forge_release_type
        changelogType = "markdown"
        changelog = rootProject.file('changelog.md')
        addGameVersion project.minecraft_version
        addGameVersion "Java 17"
        addGameVersion "Forge"

        relations {
            requiredDependency("architectury-api")
            requiredDependency("supermartijn642s-config-lib")
        }

        mainArtifact(remapJar) {
            displayName = "GriefLogger Forge $project.minecraft_version - $project.mod_version"
        }

        addArtifact(sourcesJar) {
        }
    }
}

modrinth {
    token = System.getenv("MODRINTH_API_KEY")
    projectId = "8oGVUFuX"
    versionName = "GriefLogger Forge $rootProject.minecraft_version - $rootProject.mod_version"
    versionNumber = "$rootProject.mod_version"
    versionType = "$rootProject.curse_forge_release_type"
    uploadFile = remapJar
    additionalFiles = [remapSourcesJar]
    gameVersions = ["$rootProject.minecraft_version"]
    loaders = ["forge"]
    dependencies {
        required.project "architectury-api"
        required.project "supermartijn642s-config-lib"
    }
}